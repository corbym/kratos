group 'uk.co.binarysoup'
version '1.0-SNAPSHOT'

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'org.unbroken-dome.gradle-plugins:gradle-testsets-plugin:1.0.2'
    }
}

apply plugin: 'application'
apply plugin: 'java'
apply plugin: 'org.unbroken-dome.test-sets'

repositories {
    mavenCentral()
}

sourceCompatibility = 1.8

project.ext {
    dropwizardVersion = '1.1.4'
    configPath = "$rootProject.projectDir/config/"
}

testSets {
    acceptanceTest { dirName = 'acceptance-test' }
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'io.dropwizard', name: 'dropwizard-testing', version: dropwizardVersion
    testCompile group: 'io.dropwizard', name: 'dropwizard-client', version: dropwizardVersion
    testCompile 'org.hamcrest:java-hamcrest:2.0.0.0'

    compile group: 'io.dropwizard', name: 'dropwizard-core', version: dropwizardVersion
}

project.acceptanceTest{
    exclude '**/*Test.class'
    include '**/AcceptanceTestSuite*'
    outputs.upToDateWhen {false}
}

check.dependsOn acceptanceTest
acceptanceTest.mustRunAfter test

tasks.withType(Test) {
    reports.html.destination = file("${reporting.baseDir}/${name}")
}

mainClassName = "uk.co.binarysoup.kratos.KratosApplication"

run {
    args 'server', configPath + 'app_config.yml'
}